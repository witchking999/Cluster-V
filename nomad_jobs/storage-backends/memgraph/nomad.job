job "memgraph" {
  region      = var.region
  datacenters = [var.datacenter]
  type        = "service"

  meta {
    job_file = "nomad_jobs/storage-backends/memgraph/nomad.job"
    version  = "1"
  }

  group "graph" {
    count = 1

    network {
      mode = "host"
      port "bolt" {
        static       = 7687
        host_network = "lan"
      }
      port "mgconsole" {
        static       = 7444
        host_network = "lan"
      }
      port "lab" {
        static       = 3001
        host_network = "lan"
      }
    }

    restart {
      attempts = 3
      delay    = "30s"
      interval = "10m"
      mode     = "delay"
    }

    update {
      max_parallel     = 1
      min_healthy_time = "45s"
      auto_revert      = true
    }

    task "memgraph" {
      driver = "docker"
      config {
        image  = "memgraph/memgraph-mage"
        ports  = ["bolt", "mgconsole"]
        volumes = [
          "${var.memgraph_data_dir}:/var/lib/memgraph"
        ]
      }

      service {
        name = "memgraph"
        tags = ["graph-db", "ai"]
        port = "bolt"

        check {
          type     = "tcp"
          port     = "bolt"
          interval = "30s"
          timeout  = "3s"
        }
      }

      resources {
        cpu    = 500
        memory = 4096
      }
    }

    task "memgraph-lab" {
      driver = "docker"
      config {
        image = "memgraph/lab"
        ports = ["lab"]
        env = {
          QUICK_CONNECT_MG_HOST = "memgraph.service.consul"
          QUICK_CONNECT_MG_PORT = "7687"
        }
      }

      service {
        name = "memgraph-lab"
        tags = ["traefik.enable=true"]
        port = "lab"

        check {
          type     = "tcp"
          port     = "lab"
          interval = "30s"
          timeout  = "3s"
        }
      }

      resources {
        cpu    = 300
        memory = 1024
      }
    }
  }
}

variable "region" {
  type = string
}

variable "datacenter" {
  type = string
}

variable "memgraph_data_dir" {
  type        = string
  description = "Host directory for Memgraph persistence"
  default     = "/home/shared/memgraph"
}
